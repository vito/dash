{
  package ast
}

Dash <- es:(_ e:Expr _ CommaToken? { return e, nil })* !. {
  exprs := sliceOf[Node](es)
  log.Println("!!! DASH", exprs)
  return Block{exprs}, nil
}

Expr <- Class / Slot / Form

Form <- Infix / Term

Term <- Select / FunCall / List / Literal / Symbol

Class <- ClsToken _ name:Id _ block:Block {
  return ClassDecl{
    Named: name.(string),
    Value: block.(Block),
    Visibility: PrivateVisibility, // TODO
  }, nil
}
ClsToken <- "cls"

Slot <- TypeAndArgsAndBlockSlot  // pub a(foo: Boolean!): Integer! { 1 }
      / TypeAndBlockSlot         // pub a: Integer! { 1 }  <- becomes a function that takes an empty record of args, can use other fields
      / TypeAndValueSlot         // pub a: Integer! = 1    <- no longer required, default pre-initialized (don't need to worry about mutation)
      / ValueOnlySlot            // pub a = 1              <- as above, type inferred as Integer!
      / TypeOnlySlot             // pub a: Integer              <- as above, type inferred as Integer!

TypeAndValueSlot <- vis:Visibility _ name:Id _ ColonToken _ type_:Type _ "=" _ value:Form {
  return SlotDecl{
    Named: name.(string),
    Type_: type_.(TypeNode),
    Value: value.(Node),
    Visibility: vis.(Visibility),
  }, nil
}

ValueOnlySlot <- vis:Visibility _ name:Id _ '=' _ val:Form {
  return SlotDecl{
    Named: name.(string),
    Value: val.(Node),
    Visibility: vis.(Visibility),
  }, nil
}

TypeOnlySlot <- vis:Visibility _ name:Id _ ColonToken _ type_:Type {
  return SlotDecl{
    Named: name.(string),
    Type_: type_.(TypeNode),
    Visibility: vis.(Visibility),
  }, nil
}

TypeAndBlockSlot <- vis:Visibility _ name:Id _ ColonToken _ type_:Type _ block:Block {
  return SlotDecl{
    Named: name.(string),
    Type_: FunTypeNode{nil, type_.(TypeNode)},
    Value: FunDecl{
      Named: name.(string),
      Ret: type_.(TypeNode),
      Form: block.(Block),
    },
    Visibility: vis.(Visibility),
  }, nil
}

TypeAndArgsAndBlockSlot <- vis:Visibility _ name:Id _ args:ArgTypes _ ColonToken _ type_:Type _ block:Block {
  return SlotDecl{
    Named: name.(string),
    Type_: FunTypeNode{nil, type_.(TypeNode)},
    Value: FunDecl{
      Named: name.(string),
      Args: args.([]SlotDecl),
      Ret: type_.(TypeNode),
      Form: block.(Block),
    },
    Visibility: vis.(Visibility),
  }, nil
}

Visibility <- PubToken { return PublicVisibility, nil }
            / PvtToken { return PrivateVisibility, nil }
PubToken <- "pub"
PvtToken <- "pvt"

Id <- WordToken
WordToken <- [a-zA-Z0-9]+ {
  return string(c.text), nil
}
UpperId <- UpperToken
UpperToken <- [A-Z][a-zA-Z0-9]* {
  return string(c.text), nil
}

FunCall <- name:Term args:ArgValues {
  return FunCall{name.(Node), Record(args.([]Keyed[Node]))}, nil
}

ArgValues <- '(' args:KeyValue* ')' {
  return sliceOf[Keyed[Node]](args), nil
}
ArgTypes <- '(' args:ArgType* ')' {
  return sliceOf[SlotDecl](args), nil
}
ArgType <- slot:(ArgWithDefault / ArgWithBlockDefault / ArgWithType) CommaToken? {
  return slot.(SlotDecl), nil
}
ArgWithDefault <- name:Id _ ColonToken _ type_:Type _ "=" _ value:Form {
  return SlotDecl{
    Named: name.(string),
    Type_: type_.(TypeNode),
    Value: value.(Node),
  }, nil
}
ArgWithBlockDefault <- name:Id _ ColonToken _ type_:Type _ block:Block {
  return SlotDecl{
    Named: name.(string),
    Type_: type_.(TypeNode),
    Value: block.(Block),
  }, nil
}
ArgWithType <- name:Id _ ColonToken _ type_:Type {
  return SlotDecl{
    Named: name.(string),
    Type_: type_.(TypeNode),
  }, nil
}

KeyValue <- key:WordToken ColonToken _ value:Form CommaToken? {
  return Keyed[Node]{key.(string), value.(Node)}, nil
}
ColonToken <- ':'

Type <- NonNull / NamedType / ListType / TypeVariable
NamedType <- name:UpperId {
  return NamedTypeNode{name.(string)}, nil
}
ListType <- '[' inner:Type ']' {
  return ListTypeNode{inner.(TypeNode)}, nil
}
NonNull <- inner:Type BangToken {
  return NonNullTypeNode{inner.(TypeNode)}, nil
}
TypeVariable <- v:[a-z] {
  return VariableTypeNode{v.(byte)}, nil
}

BangToken <- '!'

CommaToken <- _ ',' _

Infix <- Default
Default <- left:Form _ InterroToken _ right:Term {
  return Default{left.(Node), right.(Node)}, nil
}
InterroToken <- '?'

Select <- left:Term _ DotToken _ name:Id {
  return Select{left.(Node), name.(string)}, nil
}
DotToken <- '.'

List <- '[' _ eles:(_ e:Form CommaToken? _ { return e, nil })* ']' {
  return List{sliceOf[Node](eles)}, nil
}

Block <- '{' es:(_ e:Expr CommaToken? _ { return e, nil })* '}' {
  exprs := sliceOf[Node](es)
  log.Println("!!! BLOCK", exprs)
  return Block{exprs}, nil
}

Symbol <- name:Id {
  return Symbol{name.(string)}, nil
}

// Literals

Literal <- Integer / Boolean / String / Quoted / Null

Integer <- '0' / NonZeroDecimalDigit DecimalDigit* {
  value, err := strconv.ParseInt(string(c.text), 10, 64)
  if err != nil {
    return nil, err
  }
  return Integer(value), nil
}

Exponent <- 'e'i [+-]? DecimalDigit+

String <- '"' ( !EscapedChar . / '\\' EscapeSequence )* '"' {
  c.text = bytes.Replace(c.text, []byte(`\/`), []byte(`/`), -1)
  value, err := strconv.Unquote(string(c.text))
  if err != nil {
    return nil, err
  }
  return String{value}, nil
}

EscapedChar <- [\x00-\x1f"\\]

EscapeSequence <- SingleCharEscape / UnicodeEscape

SingleCharEscape <- ["\\/bfnrt]

UnicodeEscape <- 'u' HexDigit HexDigit HexDigit HexDigit

DecimalDigit <- [0-9]

NonZeroDecimalDigit <- [1-9]

HexDigit <- [0-9a-f]i

Quoted <- '%' quoter:WordToken '{' raw:QuotedRawToken '}' {
  return Quoted{
    quoter.(string),
    raw.(string),
  }, nil
}
QuotedRawToken <- [^}]* {
  return string(c.text), nil
}

Boolean <- TrueToken { return Boolean(true), nil }
         / FalseToken { return Boolean(false), nil }
TrueToken <- "true"
FalseToken <- "false"

Null <- NullToken { return Null{}, nil }
NullToken <- "null"

_ "whitespace" <- ([ \t\r\n] / CommentToken)*

CommentToken <- '#' [^\n]*
